<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				width="300" height="200"
				verticalScrollPolicy="off"
				horizontalScrollPolicy="off"
				close="close()"
>
	<s:Group width="100%" height="100%">
		<s:Label id="txt" horizontalCenter="0" verticalCenter="0" maxWidth="250"/>
		<mx:Button id="btnOK" label="确定" horizontalCenter="0" bottom="20" click="click(event)"/>
		<mx:Button id="btnYes" label="是" horizontalCenter="-40" bottom="20" click="click(event)"/>
		<mx:Button id="btnNo" label="否" horizontalCenter="40" bottom="20" click="click(event)"/>
	</s:Group>
	<fx:Script>
		<![CDATA[
			import mx.managers.PopUpManager;
			
			private static var alertPan:AlertPan;
			private static var onConfirm:Function;
			
			public static const TYPE_OK:int=0;
			public static const TYPE_NOTHING:int=1;
			public static const TYPE_YES_NO:int=2;
			
			public static function show(
				container:Sprite,
				title:String,
				msg:String,
				_onConfirm:Function,
				type:int=TYPE_OK
			):void{
				if(alertPan){
					alertPan.close();
				}
				alertPan=PopUpManager.createPopUp(
					container,
					AlertPan,
					true
				) as AlertPan;
				
				onConfirm=_onConfirm;
				
				alertPan.init(
					title,
					msg,
					type
				);
			}
			public static function hide():void{
				alertPan.close();
			}
			
			private function init(
				_title:String,
				msg:String,
				type:int
			):void{
				title=_title;
				txt.text=msg;
				this.x=(stage.stageWidth-this.width)/2;
				this.y=(stage.stageHeight-this.height)/2;
				switch(type){
					case TYPE_NOTHING:
						btnYes.visible=false;
						btnNo.visible=false;
						btnOK.visible=false;
						break;
					case TYPE_YES_NO:
						btnOK.visible=false;
					break;
					default://TYPE_OK
						btnYes.visible=false;
						btnNo.visible=false;
					break;
				}
			}
			private function close():void{
				//onConfirm=null;
				PopUpManager.removePopUp(this);
				alertPan=null;
			}
			
			private function click(event:MouseEvent):void{
				switch(event.target){
					case btnOK:
						close();
						(onConfirm==null)||onConfirm();
					break;
					case btnYes:
						close();
						onConfirm(true);
					break;
					case btnNo:
						close();
						onConfirm(false);
					break;
				}
			}
		]]>
	</fx:Script>
</mx:TitleWindow>
