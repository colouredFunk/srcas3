<?xml version="1.0" encoding="utf-8"?>
<!--

/***
OutputPane 版本:v1.0
简要说明:这家伙很懒什么都没写
创建人:ZЁЯ¤  身高:168cm+;体重:57kg+;未婚(已有女友);最爱的运动:睡觉;格言:路见不平,拔腿就跑;QQ:358315553
创建时间:2009年11月18日 15:36:24
历次修改:未有修改
用法举例:这家伙很懒什么都没写
*/

-->
<s:Group
 creationComplete="onCreationComplete()"
 xmlns:fx="http://ns.adobe.com/mxml/2009"
 xmlns:s="library://ns.adobe.com/flex/spark"
 xmlns:mx="library://ns.adobe.com/flex/mx"
 width="400" height="400"
>
	<mx:TextArea id="txt" wordWrap="false" left="0" top="0" right="0" bottom="0"/>
	<mx:Button label="清空" bottom="2" right="22" click="click()"/>
	<fx:Declarations>
		<fx:String id="cssStr">
			<![CDATA[
			a:link {
				color: #3399ff;
				text-decoration: underline;
			}
			a:hover {
				color: #ff9933;
				text-decoration: underline;
			}
			.red 	{color: #cc0000;}
			.green 	{color: #006600;}
			.blue 	{color: #0000cc;}
			.brown	{color: #ff6600;}
			.grey	{color: #bbbbbb;}
			.pink	{color: #ff00ff;}
			]]>
		</fx:String>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import flash.display.*;
			import flash.events.*;
			import flash.text.*;
			import flash.utils.*;
			
			import mx.core.mx_internal;
			
			private var strArr:Array;
			private var timeoutId:int=-1;
			private var linkFunArr:Array;
			private var linkId:int;
			
			private function onCreationComplete():void{
				//^_^
				linkFunArr=new Array();
				linkId=0;
				strArr=new Array();
				txt.styleSheet=new StyleSheet();
				txt.styleSheet.parseCSS(cssStr);
				txt.addEventListener(TextEvent.LINK,link);
			}
			public function clear():void{
				//^_^
				txt.removeEventListener(TextEvent.LINK,link);
				linkFunArr=null;
				txt.htmlText="";
				strArr=new Array();
				clearTimeout(timeoutId);
			}
			public function output(msg:String,styleOrXML:*=undefined,tail:*=null,linkFun:Function=null,immediately:Boolean=false):void{
				if(linkFun==null){
				}else{
					var tailXML:XML=<a href={"event:"+linkId}/>
					tailXML.appendChild(tail);
					linkFunArr[linkId]=linkFun;
					linkId++;
					tail=tailXML;
				}
				msg2strArr(msg,styleOrXML,tail);
				clearTimeout(timeoutId);
				if(immediately){
					updateDelay();
				}else{
					timeoutId=setTimeout(updateDelay,100);
				}
			}
			private function link(event:TextEvent):void{
				if(linkFunArr[event.text]){
					linkFunArr[event.text]();
					return;
				}
				
				var FileClass:*;
				try{
					FileClass=flash.utils.getDefinitionByName("flash.filesystem.File");
				}catch(e:Error){
					FileClass=null;
				}
				
				var id:int=event.text.indexOf(":");
				if(id>0){
					switch(event.text.substr(0,id)){
						case "file":
							if(FileClass){
								var file:*=new FileClass(event.text.substr(id+1));
								if(file.exists){
									if(new FileClass(FileClass.applicationDirectory.nativePath).getRelativePath(file) is String){
										outputError("不能打开 applicationDirectory 目录下的东西");
										//http://help.adobe.com/en_US/as3/dev/WS5b3ccc516d4fbf351e63e3d118666ade46-7fe4.html#WS2A7C0A31-A6A9-42d2-8772-79166A98A085
										//You cannot use the openWithDefaultApplication() method with files located in the application directory.
									}else{
										file.openWithDefaultApplication();
									}
								}else{
									outputError("目录不存在");
								}
							}else{
								outputError("不支持 File 的使用");
							}
							break;
						default:
							throw new Error("未处理的 link: "+event.text);
						break;
					}
				}
			}
			private function encodeMsg(msg:String):String{
				return msg.replace(/</g,"&lt;").replace(/>/g,"&gt;").replace(/\x00/g,"\\x00");
			}
			private function msg2strArr(msg:String,styleOrXML:*,tail:*):void{
				var execResult:Array;
				if(styleOrXML){
					var html:XML;
					switch(styleOrXML){
						case "folder and file":
							msg=encodeMsg(msg);
							execResult=/^(.*?)(file:\/\/\/.*\/)(.*?)$/.exec(msg);
							if(execResult[0]==msg){
								msg='<span class="blue">'+execResult[1]+'</span>'+
									'<a href="event:file:'+execResult[2]+'">'+execResult[2]+'</a>'+
									'<a href="event:file:'+execResult[2]+execResult[3]+'">'+execResult[3]+'</a>';
							}else{
								throw new Error("msg 格式不正确");
							}
						break;
						case "folder":
							msg=encodeMsg(msg);
							execResult=/^(.*?)(file:\/\/\/.*\/)(.*?)$/.exec(msg);
							if(execResult[0]==msg){
								msg='<span class="blue">'+execResult[1]+'</span>'+
									'<a href="event:file:'+execResult[2]+'">'+execResult[2]+'</a>'+
									'<span class="blue">'+execResult[3]+'</span>';
							}else{
								throw new Error("msg 格式不正确");
							}
						break;
						case "file":
							throw new Error("未处理");
						break;
						default:
							if(styleOrXML is XML){
								html=styleOrXML.copy();
							}else{
								html=<span class={styleOrXML}/>
							}
							html.appendChild(msg);
							msg=html.toXMLString();
						break;
					}
				}else{
					msg=encodeMsg(msg);
				}
				if(tail){
					if(tail is XML){
						msg+="　"+tail.toXMLString();
					}else{
						msg+="　"+tail;
					}
				}
				strArr[strArr.length]=msg+"<br>";
			}
			private function updateDelay():void{
				txt.htmlText=strArr.join("");
				txt.validateNow();
				txt.verticalScrollPosition=txt.mx_internal::getTextField().numLines;
				txt.validateNow();
				/*
				txt.validateNow();
				while(txt.textHeight>this.height-50&&strArr.length>0){
					strArr.shift();
					txt.htmlText=strArr.join("");
					txt.validateNow();
				}
				*/
			}
			public function outputError(msg:String):void{
				output(msg,"red");
			}
			private function click():void{
				txt.htmlText="";
				strArr=new Array();
			}
		]]>
	</fx:Script>
</s:Group>
<!--

// 常忘正则表达式
// /^\s*|\s*$/					//前后空白						"\nabc d  e 哈 哈\t \r".replace(/^\s*|\s*$/g,"") === "abc d  e 哈 哈"
// /[\\\/:*?\"<>|]/				//不合法的windows文件名字符集		"\\\/:*?\"<>|\\\/:*哈 哈?\"<>|\\哈 \/:*?\"<>|".replace(/[\\\/:*?\"<>|]/g,"") === "哈 哈哈 "
// /[a-zA-Z_][a-zA-Z0-9_]*/		//合法的变量名(不考虑中文)
// value=value.replace(/[^a-zA-Z0-9_]/g,"").replace(/^[0-9]*/,"");//替换不合法的变量名
// 先把除字母数字下划线的字符去掉,再把开头的数字去掉
// 想不到怎样能用一个正则表达式搞定...

//正则表达式30分钟入门教程		http://www.unibetter.com/deerchao/zhengzhe-biaodashi-jiaocheng-se.htm
//正则表达式用法及实例			http://eskimo.blogbus.com/logs/29095458.html
//常用正则表达式					http://www.williamlong.info/archives/433.html

/*

//常用值

//常用语句块

*/

-->