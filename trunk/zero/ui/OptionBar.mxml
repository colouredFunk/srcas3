<?xml version="1.0" encoding="utf-8"?>

<!--

/***
OptionBar
创建人：ZЁЯ¤　身高：168cm+；体重：57kg+；未婚（已有女友）；最爱的运动：睡觉；格言：路见不平，拔腿就跑。QQ：358315553。
创建时间：2011年7月28日 14:28:01
简要说明：这家伙很懒什么都没写。
用法举例：这家伙还是很懒什么都没写。
*/

-->

<s:Group
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:ui="zero.ui.*"
>
	<s:Label x="5" y="5" id="txt"/>
	<mx:ComboBox id="cb" y="0" x="45" width="190" change="change()"/>
	<mx:Button x="240" y="0" label="保存配置" id="btn" click="save()"/>
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			import zero.*;
			import zero.air.*;
			
			private var optionsFolderPath:String;
			private var in_getOptionXML:Function;
			private var out_onChangeOptionXML:Function;
			
			private var saveFile:File;
			private var settingXMLFile:File;
			private var settingXML:XML;
			
			public function clear():void{
				//^_^ 
				
				in_getOptionXML=null;
				out_onChangeOptionXML=null;
				
				if(saveFile){
					saveFile.removeEventListener(Event.SELECT,selectSave);
					saveFile=null;
				}
				settingXMLFile=null;
			}
			
			public function init(
				title:String,
				btnLabel:String,
				_optionsFolderPath:String,
				_in_getOptionXML:Function,
				_out_onChangeOptionXML:Function,
				...optionNameAndXMLArr:Array
			):void{
				// ^_^
				
				txt.text=title+"：";
				btn.label=btnLabel;
				optionsFolderPath=_optionsFolderPath;
				in_getOptionXML=_in_getOptionXML;
				out_onChangeOptionXML=_out_onChangeOptionXML;
				
				var optionsFolder:File=new File(optionsFolderPath+"options/");
				if(optionsFolder.exists){
				}else{
					optionsFolder.createDirectory();
				}
				
				saveFile=new File();
				saveFile.addEventListener(Event.SELECT,selectSave);
				
				settingXMLFile=new File(optionsFolderPath+"setting.xml");
				
				if(settingXMLFile.exists){
					settingXML=new XML(ReadAndWriteFile.readStrFromFile(settingXMLFile));
				}else{
					settingXML=<setting currOptionName="默认"/>;
					writeSettingXML();
				}
				if(getCurrOptionFile().exists){
				}else{
					settingXML.@currOptionName="默认";
					writeOptionXML(<xml/>,"默认",false);
					writeSettingXML();
				}
				
				for each(var optionNameAndXML:Array in optionNameAndXMLArr){
					writeOptionXML(optionNameAndXML[1],optionNameAndXML[0],false);
				}
				
				updateOptions();
				
				out_onChangeOptionXML(getCurrOptionXML());
			}
			
			private function updateOptions():void{
				var optionNameArr:Array=new Array();
				for each(var optionXMLFile:File in new File(optionsFolderPath+"options/").getDirectoryListing()){
					var optionXML:XML=new XML(ReadAndWriteFile.readStrFromFile(optionXMLFile));
					optionNameArr.push(optionXMLFile.name.replace(/^(.*)\.xml$/i,"$1"));
				}
				cb.dataProvider=optionNameArr;
				
				cb.selectedItem=settingXML.@currOptionName.toString();
			}
			
			public function getCurrOptionFile():File{
				return new File(optionsFolderPath+"options/"+settingXML.@currOptionName.toString()+".xml");
			}
			private function getCurrOptionXML():XML{
				return new XML(ReadAndWriteFile.readStrFromFile(getCurrOptionFile()));
			}
			
			private function change():void{
				尝试暂存当前();
				settingXML.@currOptionName=cb.selectedItem.toString();
				writeSettingXML();
				out_onChangeOptionXML(getCurrOptionXML());
			}
			private function save():void{
				saveFile.url=optionsFolderPath+"options/";
				saveFile.browseForSave("保存");
			}
			private function selectSave(event:Event):void{
				if(decodeURI(saveFile.parent.url)+"/"==optionsFolderPath+"options/"){
				}else{
					Alert.show("暂不支持保存至 options 以外的文件夹");
					return;
				}
				var optionName:String=saveFile.name.replace(/^(.*)\.xml$/i,"$1");
				switch(optionName){
					case "默认":
					case "当前":
						Alert.show('"'+optionName+'" 已经被证用，请保存为其它名字');
						return;
					break;
				}
				saveCurrOptionAndUpdate(optionName);
			}
			private function saveCurrOptionAndUpdate(optionName:String):void{
				writeOptionXML(in_getOptionXML(),optionName,true);
				settingXML.@currOptionName=optionName;
				writeSettingXML();
				updateOptions();
			}
			
			public function 尝试暂存当前():void{
				if(settingXML.@currOptionName.toString()=="当前"){
				}else{
					saveCurrOptionAndUpdate("当前");
				}
			}
			private function writeSettingXML():void{
				ReadAndWriteFile.writeStrToFile('<?xml version="1.0" encoding="utf-8"?>\n'+settingXML.toXMLString(),settingXMLFile);
			}
			private function writeOptionXML(optionXML:XML,optionName:String,overwrite:Boolean):void{
				var optionXMLFile:File=new File(optionsFolderPath+"options/"+optionName+".xml");
				if(optionXMLFile.exists){
					if(overwrite){
					}else{
						return;
					}
					if(new XML(ReadAndWriteFile.readStrFromFile(optionXMLFile)).toXMLString()==optionXML.toXMLString()){
						return;
					}
				}
				ReadAndWriteFile.writeStrToFile('<?xml version="1.0" encoding="utf-8"?>\n'+optionXML.toXMLString(),optionXMLFile);
				Outputer.output("保存 optionXML 至："+decodeURI(optionXMLFile.url),"blue");
			}
		]]>
	</fx:Script>
</s:Group>